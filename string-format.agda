{- formatted printing like printf, except type-safe (as proposed
   in "Cayenne -- a language with dependent types" by Augustsson).

   The types of the rest of the arguments are computed from the
   format string.  -}
module string-format where

open import char
open import eq
open import list
open import nat

open import nat-to-string
open import string

{- for partial evaluation of the type computed from the format
   string to make progress in tandem with the term for formatted
   printing, we need to use a view, as proposed in "The view 
   from the left" by McBride and McKinna. -}
data format-view : ğ•ƒ char â†’ Set where
  format-nat : (s : ğ•ƒ char) â†’ format-view ('%' :: 'n' :: s)
  format-string : (s : ğ•ƒ char) â†’ format-view ('%' :: 's' :: s)
  not-format : (c : char)(s : ğ•ƒ char) â†’ format-view (c :: s)
  empty-format : format-view []

format-cover : (s : ğ•ƒ char) â†’ format-view s
format-cover ('%' :: 'n' :: s) = format-nat s
format-cover ('%' :: 's' :: s) = format-string s
format-cover (c :: s) = not-format c s
format-cover [] = empty-format

format-th : ğ•ƒ char â†’ Set
format-th s with format-cover s
format-th .('%' :: 'n' :: f) | format-nat f = â„• â†’ format-th f
format-th .('%' :: 's' :: f) | format-string f = string â†’ format-th f
format-th .(c :: f) | not-format c f = format-th f
format-th .[] | empty-format = string

format-t : string â†’ Set
format-t s = format-th (string-to-ğ•ƒchar s)

format-h : ğ•ƒ char â†’ (f : ğ•ƒ char) â†’ format-th f
format-h s f with format-cover f
format-h s .('%' :: 'n' :: f) | format-nat f = Î» n â†’ format-h (s ++ (string-to-ğ•ƒchar (â„•-to-string n))) f
format-h s .('%' :: 's' :: f) | format-string f = Î» s' â†’ format-h (s ++ (string-to-ğ•ƒchar s')) f
format-h s .(c :: f) | not-format c f = format-h (s ++ [ c ] ) f
format-h s .[] | empty-format = ğ•ƒchar-to-string s

format : (s : string) â†’ format-t s
format s = format-h [] (string-to-ğ•ƒchar s)

format-type-test : â„• â†’ string â†’ string â†’ string â†’ string
format-type-test = format "%n% of the %ss are in the %s %s"

format-test1 : format "%n% of the %ss are in the %s" 25 "dog" "doghouse" â‰¡ "25% of the dogs are in the doghouse"
format-test1 = refl